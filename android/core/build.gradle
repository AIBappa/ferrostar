plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.mozilla.rust-android-gradle.rust-android'
}

// TODO: Publishing?

android {
    namespace 'com.stadiamaps.ferrostar.core'
    compileSdk 33
    ndkVersion "25.2.9519653"

    defaultConfig {
        minSdk 29
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.10.0"))

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'

    implementation "net.java.dev.jna:jna:5.9.0@aar"

    testImplementation 'junit:junit:4.13.2'

    // These probably shouldn't have to be androidTestImplementation... see rant in
    // ValhallaCoreTest.kt
    androidTestImplementation 'com.github.gmazzo:okhttp-mock:1.5.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

cargo {
    module  = "../../common"  // Directory containing Cargo.toml
    libname = "ferrostar"     // [package] name from Cargo.toml
    targets = ["arm", "arm64", "x86", "x86_64", "darwin-aarch64"]  // See https://github.com/mozilla/rust-android-gradle for a longer list of options
    // TODO: Set profile to release for release builds!
}

android.libraryVariants.all { variant ->
    def generateBindings = tasks.register("generate${variant.name.capitalize()}UniFFIBindings", Exec) {
        workingDir '../../common'
        commandLine 'cargo', 'run', '-p', 'uniffi-bindgen', 'generate', '--library', 'target/aarch64-linux-android/release/libferrostar.so', '--language', 'kotlin', '--out-dir', "${buildDir}/generated/source/uniffi/${variant.name}/java"

        dependsOn 'cargoBuild'
    }

    variant.javaCompileProvider.get().dependsOn('cargoBuild')
    variant.javaCompileProvider.get().dependsOn(generateBindings)

    def sourceSet = variant.sourceSets.find { it.name == variant.name }
    sourceSet.java.srcDir new File(buildDir, "generated/source/uniffi/${variant.name}/java")

    // UniFFI tutorial notes that they made several attempts like this but were unsuccessful coming
    // to a good solution for forcing the directory to be marked as generated (short of checking in
    // project files, I suppose).
//    idea.module.generatedSourceDirs += file("${buildDir}/generated/source/uniffi/${variant.name}/java/uniffi")

    sourceSet.java.srcDir 'src/main/java'
}